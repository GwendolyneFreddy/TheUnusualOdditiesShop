BACKUP ~abystore/backup~
//AUTHOR ~Rastor <rastor@rpgdungeon.net>~
SUPPORT ~http://www.shsforums.net/topic/57827-the-unusual-oddities-shop-v500-a-mod-from-rastor/~

VERSION ~5.0.0~

README ~abystore/readme/abystore-readme.html~

ALWAYS

	// Determines which game is installed and if the game is an Enhanced Edition.
	OUTER_SET is_ee = (GAME_IS ~bg2ee eet~) ? 1 : 0	// EE game

	// Detects 1PP compatibility
	OUTER_SET is_1pp_400    =  (MOD_IS_INSTALLED ~1pp.tp2~ ~400~) ? 1 : 0											// 1ppv4: Core updates and item patches
	OUTER_SET is_1pp_shld   = ((MOD_IS_INSTALLED ~1pp.tp2~ ~206~) AND (MOD_IS_INSTALLED ~1pp.tp2~ ~400~)) ? 1 : 0	// 1ppv4: Increased paperdoll object variety (core) with Additional Shield Animations (core)
	OUTER_SET is_1pp_staff  = ((MOD_IS_INSTALLED ~1pp.tp2~ ~207~) AND (MOD_IS_INSTALLED ~1pp.tp2~ ~400~)) ? 1 : 0	// 1ppv4: Wizards' Staves (core)
	OUTER_SET is_1pp_staff0 = ((MOD_IS_INSTALLED ~1pp.tp2~ ~204~) AND (is_1pp_staff = 0)) ? 1 : 0					// 1ppv4: Colourable Quarterstaves without 1ppv4: Wizards' Staves (core)

	ACTION_IF (is_ee OR is_1pp_400 OR is_1pp_shld OR is_1pp_staff OR is_1pp_staff0) BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/1pp_macros.tpa~					// Loads 1PP macros dealing with items coloring.
		LAM ~Locations~												// DEFINES location field values needed to alter items' colors.
		INCLUDE ~%MOD_FOLDER%/lib/1pp_compatibility.tph~			// Loads functions restoring items coloration for EE games or if 1PP relevant components are installed.
	END

	/* Loads in any shared functions library (general macros and functions) */
	INCLUDE ~abystore/lib/gw_functions.tpa~	// Loads macros and functions library dealing with tooltip appending and specific EE items descriptions.

	/* ---------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                     *
	 *    No exception, everything needs to be converted                                              *
	 * -----------------------------------------------------------------------*---------------------- */
	ACTION_DEFINE_ARRAY r#_reload BEGIN abysetup END
	ACTION_DEFINE_ARRAY r#_noconvert BEGIN END
		LAF HANDLE_CHARSETS
		INT_VAR
		infer_charset = 1
		STR_VAR
		tra_path = EVAL ~%MOD_FOLDER%/lang~
		noconvert_array = r#_noconvert
		reload_array = r#_reload
	END

END


AUTO_TRA ~abystore/lang/%s~

LANGUAGE ~English~
		 ~english~
		 ~abystore/lang/english/abysetup.tra~

LANGUAGE ~Polski (translation by Riklaunim)~
		 ~polish~
		 ~abystore/lang/polish/abysetup.tra~

LANGUAGE ~Russian (translation by AERIE.ru)~
		 ~russian~
		 ~abystore/lang/russian/abysetup.tra~

LANGUAGE ~Francais (traduction : Mathrim Cauthon)~
		 ~french~
		 ~abystore/lang/french/abysetup.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
		 ~italian~
		 ~abystore/lang/italian/abysetup.tra~

LANGUAGE ~Deutsch (Uebersetzung von the BiG World Textpack German team)~
		 ~german~
		 ~abystore/lang/german/abysetup.tra~


/* ========================================== *
 *         The Unusual Oddities Shop          *
 * ========================================== */

BEGIN @100
DESIGNATED 0 LABEL ~the_unusual_oddities_shop~
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @101

/*DEPRECATED as of v5.0.0
ACTION_IF FILE_EXISTS ~override/r#abystore.rpgd~ BEGIN
	FAIL ~The Unusual Oddities Shop is already installed.~
END ELSE BEGIN
	COPY_EXISTING ~sw1h01.itm~ ~override/r#abystore.rpgd~
END
*/

// Orc Render
// ----------
COPY ~abystore/itm/r#asoswd.itm~ ~override~
//	SAY NAME1 @1	SAY UNIDENTIFIED_DESC @3
	SAY NAME2 @2
 	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@4" END
BUT_ONLY

// Loren's Chain +5
// ----------------
COPY ~abystore/itm/r#aspchn.itm~ ~override~
//	SAY NAME1 @5	SAY UNIDENTIFIED_DESC @7
	SAY NAME2 @6
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@8" END
BUT_ONLY

// Shield of the Lords +4
// ----------------------
COPY ~abystore/itm/r#asshld.itm~ ~override~
//	SAY NAME1 @9	SAY UNIDENTIFIED_DESC @11
	SAY NAME2 @10
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@12" END
	PATCH_IF (is_ee OR is_1pp_shld) BEGIN
		LPF ~1pp_shld32~ END
	END
BUT_ONLY

// Cloak of the Gods
// ----------------
COPY ~abystore/itm/r#asgclk.itm~ ~override~
//	SAY NAME1 @13	SAY UNIDENTIFIED_DESC @15
	SAY NAME2 @14
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@16" END
BUT_ONLY

// Minor Mage Gauntlets
// --------------------
COPY ~abystore/itm/r#asmgau.itm~ ~override~
//	SAY NAME1 @17	SAY UNIDENTIFIED_DESC @19
	SAY NAME2 @18
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@20" END
BUT_ONLY

// Staff of the Dragon +3
// ----------------------
COPY ~abystore/itm/r#asmstf.itm~ ~override~
//	SAY NAME1 @21	SAY UNIDENTIFIED_DESC @23
	SAY NAME2 @22
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@24" END
	PATCH_IF (is_ee OR is_1pp_staff) BEGIN
		LPF ~1pp_staf23~ END
	END
	PATCH_IF (is_1pp_staff0) BEGIN
		LPF ~1pp_staf23_0~ END
	END
BUT_ONLY

// Halberd of Knowing +5
// ---------------------
COPY ~abystore/itm/r#ashalb.itm~ ~override~
//	SAY NAME1 @25	SAY UNIDENTIFIED_DESC @27
	SAY NAME2 @26
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@28" END
	PATCH_IF (is_ee OR is_1pp_400) BEGIN
		LPF ~1pp_halb10~ END
	END
BUT_ONLY

// Abysima
// -------
COPY ~abystore/cre/r#asdemn.cre~ ~override~
	SAY NAME1 @29
	SAY NAME2 @30
	WRITE_ASCII DIALOG ~R#ASDEMN~
BUT_ONLY

// Install dialog and script
// -------------------------
COMPILE ~abystore/dlg/abysima.d~

EXTEND_TOP ~ar0300.bcs~ ~abystore/baf/abyarea.baf~

// Abysima's Store
// ---------------
COPY ~abystore/store/r#asdemn.sto~ ~override~
	SAY NAME2 @31
BUT_ONLY
